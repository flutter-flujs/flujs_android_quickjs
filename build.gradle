group "cn.humphreyd.flujs"
version "0.6.1"

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:8.1.0"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: "com.android.library"
apply plugin: "maven-publish"

android {
    if (project.android.hasProperty("namespace")) {
        namespace = "cn.humphreyd.flujs"
    }
    compileSdkVersion 31
    ndkVersion = android.ndkVersion
    defaultConfig {
        minSdk = 16
    }
    flavorDimensions "abi"
    productFlavors {
        x86 {
            dimension "abi"
        }
        arm {
            dimension "abi"
        }
    }
    sourceSets {
        arm.jniLibs.srcDirs += "src/arm/jniLibs"
        x86.jniLibs.srcDirs += "src/x86/jniLibs"
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = "cn.humphreyd.flujs"
            artifactId = "quickjs"
            version = "0.6.1"

            pom {
                name = "quickjs"
                description = "quickjs-ng for flujs."
                url = "https://github.com/flutter-flujs/flujs_android_quickjs"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "dang8080@qq.com"
                        name = "ShuttleSpace"
                        email = "dang8080@qq.com"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/flutter-flujs/flujs_android_quickjs.git"
                    developerConnection = "scm:git:https://github.com/flutter-flujs/flujs_android_quickjs.git"
                    url = "https://github.com/flutter-flujs/flujs_android_quickjs"
                }
            }
        }
    }
    repositories {
        maven {
            name = "quickjs"
            url = uri("https://maven.pkg.github.com/flutter-flujs/flujs_android_quickjs")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}